set dotenv-load

[no-cd]
develop:
  DEVENV_DIR=$(pwd) nix develop

env TYPE NAME:
  (cd ~/dev/src/{{TYPE}}/{{NAME}} && just -g develop)

[no-cd]
up *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml up --build -d {{SERVICE}}

[no-cd]
down *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml down {{SERVICE}}

[no-cd]
restart SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml restart {{SERVICE}}

[no-cd]
log *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml logs --timestamps -f {{SERVICE}}

[no-cd]
create-env NAME ENV *COPYCERTS='false':
  #!/usr/bin/env bash

  cp -r $NIXCONFIG/dev-envs/{{ENV}} {{NAME}}
  find {{NAME}}/ -type f | xargs -I{} sed -i 's/projectname/{{NAME}}/g' {}
  
  if [ -d {{NAME}}/.devcontainer ]; then
    if {{COPYCERTS}}; then
      cp $HOME/.local/share/mkcert/localhost.pfx {{NAME}}/.devcontainer/
      cp $HOME/.local/share/mkcert/rootCA-key.pem {{NAME}}/.devcontainer/ca-key.crt
      chmod +w {{NAME}}/.devcontainer/localhost.pfx
    fi

    if [ -f {{NAME}}/.devcontainer/post-create.sh ]; then
      chmod +x {{NAME}}/.devcontainer/post-create.sh
    fi
  else
    echo "DEVENV_PROJECT_ROOT={{NAME}}" >> {{NAME}}/.env
  fi
