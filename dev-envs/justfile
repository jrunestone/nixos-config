set dotenv-load

[no-cd]
develop:
  DEVENV_DIR=$(pwd) nix develop

env TYPE NAME:
  (cd ~/dev/src/{{TYPE}}/{{NAME}} && just -g develop)

[no-cd]
up *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml up --build -d {{SERVICE}}

[no-cd]
down *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml down {{SERVICE}}

[no-cd]
restart SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml restart {{SERVICE}}

[no-cd]
log *SERVICE:
  podman compose --file $DEVENV_PROJECT_ROOT/docker-compose.yml logs --timestamps -f {{SERVICE}}

[no-cd]
create-bare-env NAME ENV:
  cp -r $NIXCONFIG/dev-envs/{{ENV}} {{NAME}}
  find {{NAME}}/ -type f | xargs -I{} sed -i 's/projectname/{{NAME}}/g' {}
  echo "DEVENV_PROJECT_ROOT=." >> {{NAME}}/.env

[no-cd]
create-env NAME REPO ENV:
  #!/usr/bin/env bash
  cp -r $NIXCONFIG/dev-envs/{{ENV}} {{NAME}}
  sed -i "s/jr-home/$HOSTNAME/g" {{NAME}}/flake.nix
  find {{NAME}}/ -type f | xargs -I{} sed -i 's/projectname/{{NAME}}/g' {}
  REPO_DIR_NAME=$(echo {{REPO}} | sed 's/^.*\/\(.*\)\.git$/\1/')
  echo "DEVENV_PROJECT_ROOT=$REPO_DIR_NAME" >> {{NAME}}/.env
  echo "$REPO_DIR_NAME/" >> {{NAME}}/.gitignore
  (cd {{NAME}} && git clone {{REPO}})

